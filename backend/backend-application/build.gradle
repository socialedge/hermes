/*
 * Hermes - The Municipal Transport Timetable System
 * Copyright (c) 2016-2017 SocialEdge
 * <p>
 * This program is free software: you can redistribute it and/or modify
 * it under the terms of the GNU General Public License as published by
 * the Free Software Foundation, either version 3 of the License, or
 * (at your option) any later version.
 * <p>
 * This program is distributed in the hope that it will be useful,
 * but WITHOUT ANY WARRANTY; without even the implied warranty of
 * MERCHANTABILITY or FITNESS FOR A PARTICULAR PURPOSE.  See the
 * GNU General Public License for more details.
 */

def OPENAPI_SOURCE = "$rootDir/openapi2-hermes-v2.yaml"

apply plugin: 'org.springframework.boot'
apply plugin: 'org.hidetake.swagger.generator'

dependencies {
    compile project(':backend-transit')
    compile project(':backend-schedule')
    compile project(':backend-timetable')

    compile libraries.application.springBootWeb

    compile libraries.application.swagger

    compile libraries.persistence.embeddedMongo
    compile libraries.persistence.mongeez

    compile libraries.extension.jacksonDatatypeJsr310

    swaggerCodegen libraries.gen.swaggerCodegenCli
}

jar {
    manifest {
        attributes("Implementation-Title": "Hermes",
                   "Implementation-Vendor": "socialedge.eu",
                   "Implementation-Version": version)
    }
}

swaggerSources {
    hermes {
        inputFile = file(OPENAPI_SOURCE)
        code {
            language = 'spring'
            configFile = file('swagger.json')
            components = [
                models: true, apis: true,
                supportingFiles: 'SwaggerDocumentationConfig.java'
            ]
        }
    }
}
compileJava.dependsOn swaggerSources.hermes.code
sourceSets.main.java.srcDirs += file("$buildDir/swagger-code-hermes/src/main/java")

task stage(type: Copy, dependsOn: [clean, assemble]) {
    println(jar)
    from jar.archivePath
    into jar.destinationDir
    rename {
        'app.jar'
    }
}
stage.mustRunAfter(clean)
